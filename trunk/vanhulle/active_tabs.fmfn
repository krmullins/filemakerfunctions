/*
GetActiveTabs ( init ; theList )
RETURNS:  List of all active frontmost tabs 
*/

Let([ 
//------------------------- VARIABLES
Init = Init + 1 ; 

ObjectList = LayoutObjectNames( Get( FileName ); Get( LayoutName ) ) ; 

TabToCheck = Substitute( GetValue( ObjectList; init ); ¶; "") ; 

ParentTab = GetLayoutObjectAttribute( TabToCheck; "enclosingObject" ) ; 

IsCurrentTab = Case(
                    IsEmpty( TabToCheck );  0;
                    GetLayoutObjectAttribute( TabToCheck ; "isFrontTabPanel" ) and
                        IsEmpty( ParentTab ); 1;
                    GetLayoutObjectAttribute( TabToCheck; "isFrontTabPanel" ) and
                        PatternCount( ¶ & theList & ¶; ¶ &  ParentTab & ¶ ); 1;
                    0 ); 

theList = theList & If( IsCurrentTab; TabToCheck & ¶ ) 

];

//------------------------- RESULT

If( not IsEmpty( TabToCheck ); GetActiveTabs( init; theList ); theList ) 

)


/*
Example Usage: GetActiveTabs ( 0 ; "" )

Developer: Koen Van Hulle (SHpartners.com)

Version History
==========
20070106– Original version
*/